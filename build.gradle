buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("net.serenity-bdd:serenity-gradle-plugin:4.2.16")
    classpath("au.com.dius.pact:au.com.dius.pact.gradle.plugin:4.4.0-beta.1")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'pmd'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'info.solidsoft.pitest' version '1.15.0'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.owasp.dependencycheck' version '10.0.4'
  id 'org.sonarqube' version '6.2.0.5505'
  id 'org.springframework.boot' version '2.7.18'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'au.com.dius.pact' version '4.6.17'
  id "io.freefair.lombok" version "6.6.3"
  id "name.remal.randomize-test-classes" version "1.5.0"
  id 'org.gradle.test-retry' version '1.6.2'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.serenity-gradle-plugin'
apply plugin: 'project-report'
apply plugin: 'idea'

def versions = [
  junit              : '5.13.4',
  junitPlatform      : '1.13.4',
  gradlePitest       : '1.15.0',
  pitest             : '1.19.4',
  reformLogging      : '6.1.9',
  reformHealthStarter: '0.0.5',
  restAssured        : '5.4.0',
  serenity           : '4.2.31',
  pact_version       : '4.6.17',
  sonarPitest        : '0.5',
  springDoc          : '1.8.0',
  springCloud        : '3.1.9'
]

mainClassName = 'uk.gov.hmcts.reform.iacasedocumentsapi.Application'
def jarName = 'ia-case-documents-api.jar'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked"
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-launcher:${versions.junitPlatform}"
  ]
]

sourceSets {

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

processFunctionalTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processIntegrationTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processContractTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
    testSourceDirs += project.sourceSets.contractTest.java.srcDirs
    testResourceDirs += project.sourceSets.smokeTest.resources.srcDirs
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestCompile.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  functionalTestImplementation.extendsFrom testImplementation
  contractTestImplementation.extendsFrom testImplementation
  functionalTestRuntime.extendsFrom testRuntime
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.12.5'
  configDirectory.set(new File(rootDir, 'config/checkstyle'))
}


pmd {
  toolVersion = '6.55.0'
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = [
    'category/java/errorprone.xml',
    'category/java/bestpractices.xml'
  ]
}

tasks.register('integration', Test) {
  description = 'Runs the integration tests.'
  group = 'Verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.register('functional', Test) {
  description = 'Runs the functional tests.'
  group = 'Verification'
  systemProperty "scenario", System.getProperty('scenario')
  systemProperty "file.encoding", "UTF-8"

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
  testLogging {
    showStackTraces = true
    showStandardStreams = true
    showCauses = true
    showExceptions = true
  }
}

tasks.register('smoke', Test) {
  description = 'Runs the smoke tests.'
  group = 'Verification'
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}


tasks.register('contract', Test) {
  description = "Runs the consumer Pact tests"
  group = 'Verification'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

tasks.register('fortifyScan', JavaExec) {
  description = 'Runs fortify scan.'
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

jacoco {
  toolVersion = "0.8.13"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required.set(true)
    csv.required.set(false)
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}
pitest {
//    print('./gradlew --version'.execute().text.trim())
  junit5PluginVersion = '1.1.2'
  targetClasses = ['uk.gov.hmcts.reform.iacasedocumentsapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.iacasedocumentsapi.infrastructure.config.*',
    'uk.gov.hmcts.reform.iacasedocumentsapi.Application',
    'uk.gov.hmcts.reform.iacasedocumentsapi.CcdScenarioRunnerTest'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  failWhenNoMutations = false
  verbose = true
  mutationThreshold = 85
}

project.tasks['pitest'].group = "Verification"

project.ext {
  pacticipant = 'ia_caseDocumentsApi'
  if (System.env.GIT_COMMIT) {
    pacticipantVersion = System.env.GIT_COMMIT.substring(0, 9)
  } else {
    pacticipantVersion = getCheckedOutGitCommitHash()
  }
  set('springCloudVersion', versions.springCloud)
}

tasks.register('runAndPublishConsumerPactTests', Test) {
  description = 'Publish pact test to broker.'
  group = 'Verification'
  onlyIf {
    return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
  }
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.rootDir', "pacts"
  testLogging.showStandardStreams = true
}

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    version = project.pacticipantVersion + '_' + System.nanoTime().toString()
  }
}

pactPublish {
  pactPublish = project.pact.publish
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

jacoco {
  toolVersion = "0.8.13"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required.set(true)
    csv.required.set(false)
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}

pitest {
//    print('./gradlew --version'.execute().text.trim())
  junit5PluginVersion = '1.2.3'
  pitestVersion = "1.16.2"
  targetClasses = ['uk.gov.hmcts.reform.iacasedocumentsapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.iacasedocumentsapi.infrastructure.config.*',
    'uk.gov.hmcts.reform.iacasedocumentsapi.Application',
    'uk.gov.hmcts.reform.iacasedocumentsapi.infrastructure.TsvStringProvider'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  failWhenNoMutations = false
  verbose = true
  mutationThreshold = 85
}

project.tasks.named('pitest') { group = "Verification" }

sonarqube {
  properties {
    property "sonar.projectName", "IA :: Immigration & Asylum case documents api"
    property "sonar.projectKey", "IACASEDOCUMENTSAPI"
    property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/infrastructure/config/**," +
      "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/infrastructure/SystemDateProvider.java," +
      "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/Application.java"
    property "sonar.cpd.exclusions", "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/domain/entities/AsylumCase.java," +
      "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/domain/entities/AsylumCaseBuilder.java," +
      "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/domain/templates/**" +
      "src/main/java/uk/gov/hmcts/reform/iacasedocumentsapi/domain/personalisation/**"
  }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

project.tasks.named('sonarqube') {
  dependsOn test, integration, jacocoTestReport
  group = "Verification"
}
repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

dependencyManagement {
  dependencies {

    dependencySet(group: 'org.yaml', version: '2.4') {
      entry 'snakeyaml'
    }

    dependencySet(group: 'commons-fileupload', version: '1.6.0') {
      entry 'commons-fileupload'
    }

    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.19.2') {
      entry 'jackson-databind'
      entry 'jackson-core'
      entry 'jackson-annotations'
    }

    dependencySet(group: 'com.fasterxml.jackson.dataformat', version: '2.19.2') {
      entry 'jackson-dataformat-yaml'
      entry 'jackson-dataformat-csv'
    }

    dependencySet(group: 'com.fasterxml.jackson.datatype', version: '2.19.2') {
      entry 'jackson-datatype-jsr310'
      entry 'jackson-datatype-jdk8'
    }

  }
}

dependencies {
  modules {
    module("org.codehaus.groovy:groovy") {
      replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
    }
    module("org.codehaus.groovy:groovy-xml") {
      replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
    }
    module("org.codehaus.groovy:groovy-json") {
      replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
    }
  }

  implementation('org.apache.groovy:groovy-xml:4.0.23')
  configurations.configureEach {
    resolutionStrategy {
      force 'org.apache.groovy:groovy-xml:4.0.23'
    }
  }

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloud) {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: versions.springCloud) {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'

  // CVE-2019-17195
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.48'

  // CVE-2021-27568
  implementation group: 'net.minidev', name: 'json-smart', version: '2.5.2'

  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.9.3'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
  implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc

  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'

  implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.9'
  implementation(group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation(group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }

  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
  implementation group: 'commons-io', name: 'commons-io', version: '2.20.0'

  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
  implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.81'

  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.112'
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.112'

  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'
  implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.3.0'

  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.2'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.12.4'
  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    exclude group: "com.vaadin.external.google", module: "android-json"
  }
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.38'
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.38'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
  testImplementation "org.codehaus.sonar-plugins:sonar-pitest-plugin:${versions.sonarPitest}"
  testImplementation "info.solidsoft.gradle.pitest:gradle-pitest-plugin:${versions.gradlePitest}"

  testImplementation("io.rest-assured:rest-assured-all:${versions.restAssured}") {
    exclude group: 'org.apache.groovy', module: 'groovy-xml'
  }
  testImplementation("io.rest-assured:rest-assured:${versions.restAssured}") {
    exclude group: 'org.apache.groovy', module: 'groovy-xml'
  }
  testImplementation group: 'org.springframework.security', name: 'spring-security-test'

  testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "1.9.0"
  testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-common', version: "1.9.0"
  testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7', version: "1.9.0"
  testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: "1.9.0"

//  testImplementation group: 'org.pitest', name: 'pitest', version: '1.16.2'
  testImplementation group: 'io.rest-assured', name: 'rest-assured-all', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured

  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity

  testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.7.2'
  testImplementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'

  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.9', classifier: 'all', {
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
  }
  testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'

  testImplementation 'pl.pragmatists:JUnitParams:1.1.1'

  testImplementation(group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.10') {
    exclude group: "com.github.tomakehurst", module: "wiremock-standalone"
  }

  //pact contract testing
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

gradle.startParameter.continueOnFailure = true

bootJar {
  setArchiveFileName(jarName)
  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed appllications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8", "-Dhttps.protocols=TLSv1.2"]

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
