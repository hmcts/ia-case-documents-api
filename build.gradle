buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.yaml:snakeyaml:1.27")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.11")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'info.solidsoft.pitest' version '1.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.owasp.dependencycheck' version '6.5.3'
    id 'org.sonarqube' version '3.0'
    id 'org.springframework.boot' version '2.7.0'
    id 'uk.gov.hmcts.java' version '0.12.5'
    id 'io.freefair.lombok' version '5.3.0'
    id 'au.com.dius.pact' version '4.1.7'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'

def versions = [
        junit              : '5.8.2',
        junitPlatform      : '1.7.2',
        gradlePitest       : '1.5.1',
        pact_version       : '4.1.7',
        pitest             : '1.5.2',
        reformLogging      : '5.1.7',
        reformHealthStarter: '0.0.5',
        serenity           : '3.1.20',
        sonarPitest        : '0.5',
        springBoot         : '2.7.0',
        springHystrix      : '2.2.10.RELEASE',
        springDoc          : '1.6.8',
        springSecurity     : '5.7.1',
        springCloud        : '3.1.1',
        jackson            : '2.13.3'
]

ext.libraries = [
        junit5: [
                "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
                "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
                "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
                "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
                "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
        ]
]

mainClassName = 'uk.gov.hmcts.reform.iacasenotificationsapi.Application'
def jarName = 'ia-case-notifications-api.jar'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }

    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testResourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.40'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = '6.41.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

    systemProperty "scenario", System.getProperty('scenario')

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

pitest {
    //adds dependency to org.pitest:pitest-junit5-plugin and sets "testPlugin" to "junit5"
    junit5PluginVersion = '0.15'
    targetClasses = ['uk.gov.hmcts.reform.iacasenotificationsapi.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.iacasenotificationsapi.infrastructure.config.*',
            'uk.gov.hmcts.reform.iacasenotificationsapi.Application',
            'uk.gov.hmcts.reform.iacasenotificationsapi.CcdScenarioRunnerTest'
    ]
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    failWhenNoMutations = false
    mutationThreshold = 90
}

project.tasks['pitest'].group = "Verification"

project.ext {
    pacticipant = 'ia_caseNotificationsApi'
    pacticipantVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

task runContractTests(type: Test, description: 'Runs contract tests.', group: 'Verification') {
    onlyIf {
        return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
    }
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
    testLogging.showStandardStreams = true

}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
    logger.lifecycle("Runs pact consumer Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        version = project.pacticipantVersion
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "IA :: Immigration & Asylum case notifications api"
        property "sonar.projectKey", "IACASENOTIFICATIONSAPI"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iacasenotificationsapi/infrastructure/config/**," +
                "src/main/java/uk/gov/hmcts/reform/iacasenotificationsapi/infrastructure/SystemDateProvider.java," +
                "src/main/java/uk/gov/hmcts/reform/iacasenotificationsapi/Application.java"
        property "sonar.cpd.exclusions", "src/main/java/uk/gov/hmcts/reform/iacasenotificationsapi/domain/entities/AsylumCase.java," +
                "src/main/java/uk/gov/hmcts/reform/iacasenotificationsapi/domain/entities/AsylumCaseBuilder.java," +
                "src/main/java/uk/gov/hmcts/reform/iacasenotificationsapi/domain/personalisation/**"

    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()

    maven {
        url 'https://www.jitpack.io'
    }
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.63') {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-el'
            entry 'tomcat-embed-websocket'
        }

        //CVE-2021-42550
        dependencySet(group: 'ch.qos.logback', version: '1.2.10') {
            entry 'logback-classic'
            entry 'logback-core'
        }

        //CVE-2021-22060
        dependencySet(group: 'org.springframework', version: '5.3.20') {
            entry 'spring-core'
            entry 'spring-aop'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-web'
        }

        //CVE-2021-22119
        dependency group: 'org.springframework.security', name: 'spring-security-core',  version: versions.springSecurity

        //CVE-2021-44228, CVE-2021-44832, CVE-2021-45046
        dependencySet(group: 'org.apache.logging.log4j', version: '2.17.1') {
            entry 'log4j-api'
            entry 'log4j-to-slf4j'
        }
    }
}

dependencies {

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloud
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: versions.springCloud
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot


    compile group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: versions.springSecurity


    // CVE-2019-17195
    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.22'

    // CVE-2021-27568
    compile group: 'net.minidev', name: 'json-smart', version: '2.4.8'

    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.6.2'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: versions.jackson

    compile group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc

    compile group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'

    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.1.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.2'
//    compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: versions.reformHealthStarter

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compile group: 'commons-io', name: 'commons-io', version: '2.8.0'

    compile group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.17.2-RELEASE'

    compile group: 'com.google.guava', name: 'guava', version: '30.0-jre'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.26'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

    compile group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '5.8.1'

    testCompile 'com.github.tomakehurst:wiremock:2.23.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.6.28'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    testCompile "ru.lanwen.wiremock:wiremock-junit5:1.3.1"

    testCompile group: 'org.pitest', name: 'pitest', version: versions.pitest
    testCompile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.6'
    testCompile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
    testCompile group: 'org.yaml', name: 'snakeyaml', version: '1.26'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: versions.springSecurity

    testCompile group: 'io.rest-assured', name: 'rest-assured-all', version: '4.3.1'
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '4.3.1'

    testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testCompile 'com.github.hmcts:fortify-client:1.2.1:all'

    testCompile 'pl.pragmatists:JUnitParams:1.1.1'

    testCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.26') {
        force = true
    }

    integrationTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.26') {
        force = true
    }

    functionalTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.26') {
        force = true
    }

    integrationTestCompile sourceSets.main.runtimeClasspath
    integrationTestCompile sourceSets.test.runtimeClasspath

    functionalTestCompile sourceSets.main.runtimeClasspath
    functionalTestCompile sourceSets.test.runtimeClasspath

    smokeTestCompile sourceSets.main.runtimeClasspath
    smokeTestCompile sourceSets.test.runtimeClasspath

    contractTestCompile group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestCompile group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

    contractTestCompile("org.junit.jupiter:junit-jupiter-api:5.7.0")
    contractTestRuntime("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.3.2')

    contractTestCompile sourceSets.main.runtimeClasspath
    contractTestCompile sourceSets.test.runtimeClasspath
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

gradle.startParameter.continueOnFailure = true

bootJar {
    archiveName = jarName
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed appllications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8", "-Dhttps.protocols=TLSv1.2"]

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

